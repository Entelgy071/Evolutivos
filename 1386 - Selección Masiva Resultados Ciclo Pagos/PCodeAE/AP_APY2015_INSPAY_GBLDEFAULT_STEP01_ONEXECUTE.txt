PYCYCL_2_AET.CK_PROC_INSTANCE = 0;
PYCYCL_2_AET.CK_OPRID = %OperatorId;
PYCYCL_2_AET.CK_CANCEL_ACTION = "N";
AP_PAYSELCT_AET.RECON_CYCLE_NBR = 0;
PYCYCL_2_AET.CK_CASH_CLEARD_FLG = "N";

/* Prenote */
If (PYCYCL_2_AET.CK_PYMNT_STATUS = "E") Then
   PYCYCL_2_AET.TOTAL_PAID_AMT = 0;
   PYCYCL_2_AET.PYMNT_AMT = 0;
End-If;

/* Zero payment */
If PYCYCL_2_AET.TOTAL_PAID_AMT = 0 Then
   AP_PAYSELCT_AET.POSITIVE_PAY_STAT = "Z";
   AP_PAYSELCT_AET.RECON_STATUS = "REC";
   PYCYCL_4_AET.RECONCILE_OPRID = %OperatorId;
Else
   AP_PAYSELCT_AET.POSITIVE_PAY_STAT = "N";
   If PYCYCL_2_AET.CK_PYMNT_STATUS = "O" Then
      AP_PAYSELCT_AET.RECON_STATUS = "REC";
   Else
      AP_PAYSELCT_AET.RECON_STATUS = "UNR";
   End-If;
   PYCYCL_4_AET.RECONCILE_OPRID = " ";
End-If;

/* If user approval is enabled, set in_process_flg so it will not get selected on Payment Posting */
If PYCYCL_3_AET.TRIAL_REG_FLG = "U" Then
   AP_PAYSELCT_AET.IN_PROCESS_FLG = "Y"
Else
   AP_PAYSELCT_AET.IN_PROCESS_FLG = "N"
End-If;

/* Payment confirmation */
If PYCYCL_2_AET.EXPRESS_PYMNT_FLG = "Y" Then
   PYCYCL_2_AET.CK_FORM_NBR_CNFRMD = "Y";
Else
   If PYCYCL_2_AET.CK_PYMNT_METHOD = "CHK" And
         AP_PAYSELCT_AET.FORM_NBR_CONFIRM = "Y" Then
      If PYCYCL_2_AET.CK_PYMNT_TYPE <> "Z" Then
         PYCYCL_2_AET.CK_FORM_NBR_CNFRMD = "N";
      Else
         PYCYCL_2_AET.CK_FORM_NBR_CNFRMD = "Y";
      End-If;
   End-If;
   
   If PYCYCL_2_AET.CK_PYMNT_METHOD <> "CHK" Or
         AP_PAYSELCT_AET.FORM_NBR_CONFIRM <> "Y" Then
      PYCYCL_2_AET.CK_FORM_NBR_CNFRMD = "Y";
   End-If;
End-If;

/* PYCYCL_2_AET.CHAR_FIELD = Substitute(PYCYCL_2_AET.CK_BANK_ACCT_NUM, "-", "D");
PYCYCL_2_AET.CHAR_FIELD = "C" | RTrim(PYCYCL_2_AET.CK_PYMNT_REF_NUM) | "C" | " " | "A" | RTrim(AP_PAYSELCT_AET.DFI_ID_NUM) | "A" | " " | RTrim(PYCYCL_2_AET.CHAR_FIELD) | "C"; */

AP_PAYSELCT_AET.TRANSIT_NUM = RTrim(AP_PAYSELCT_AET.DFI_ID_NUM);

/* Doc type */
PYCYCL_2_AET.CK_DOC_TYPE = AP_PAYSELCT_AET.DOC_TYPE;
If AP_PAYSELCT_AET.DOC_TYPE = " " Then
   PYCYCL_2_AET.CK_DOC_SEQ_DATE = PYCYCL_2_AET.CK_CREATION_DT;
End-If;
PYCYCL_2_AET.CK_DOC_SEQ_NBR = " ";
PYCYCL_2_AET.CK_DOC_SEQ_STATUS = "N";

/* Draft status */
Evaluate PYCYCL_2_AET.CK_PYMNT_METHOD
When = "BOO"
When = "DFT"
When = "BEF"
   PYCYCL_2_AET.DFT_STATUS = "C";
   &TODAY = %Date;
   &SETID = GetSetId("BUSINESS_UNIT", PYCYCL_2_AET.CK_BUS_UNIT_GL, "BUS_UNIT_OPT_AP", "");
   SQLExec("SELECT A.DFT_APPROVE_FLAG FROM PS_BUS_UNIT_OPT_AP A WHERE A.SETID = :1 AND A.EFF_STATUS = 'A' AND %EffDtCheck(BUS_UNIT_OPT_AP B, A, :2)", &SETID, &TODAY, &DftApprFlg);
   If &DftApprFlg = "Y" Then
      PYCYCL_2_AET.DFT_APPROVE_FLAG = "Y";
   Else
      PYCYCL_2_AET.DFT_APPROVE_FLAG = "N";
   End-If;
   Break;
When-Other
   PYCYCL_2_AET.DFT_STATUS = "N";
End-Evaluate;

/* Settlement date */
Evaluate PYCYCL_2_AET.CK_PYMNT_METHOD
When = "CHK"
When = "BOO"
   SQLExec("SELECT 'X' FROM PS_INSTALLATION WHERE AP <> 'Y'", AP_PAYSELCT_AET.DUMMY_FIELD);
   If AP_PAYSELCT_AET.DUMMY_FIELD = "X" Then
      AP_PAYSELCT_AET.STTLMNT_DT_EST = %Date;
   End-If;
   Break;
When = "WIR"
   AP_PAYSELCT_AET.STTLMNT_DT_EST = PYCYCL_2_AET.CK_PYMNT_DT;
   Break;
End-Evaluate;

/* Value date */
If None(PYCYCL_2_AET.VALUE_DT) Then
   PYCYCL_2_AET.VALUE_DT = PYCYCL_2_AET.CK_PYMNT_DT;
End-If;
